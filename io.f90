MODULE IOModule
 USE GlobalDataModule
 IMPLICIT NONE
CONTAINS
 SUBROUTINE ReadInputFile ()
  INTEGER                 :: i , j
  CHARACTER ( LEN = 5 ) :: VariableAtomName
  REAL ( KIND = 8 )       :: DPValue
  READ ( 5 , * ) XYZFileName
  READ ( 5 , * ) ParameterFileName
  READ ( 5 , * ) WantBonds
  READ ( 5 , * ) WantAngles
  READ ( 5 , * ) WantDihedrals
  READ ( 5 , * ) WantImpropers
  READ ( 5 , * ) BondLengthTolerance !in Angstrom
  READ ( 5 , * ) AngleAngleTolerance !in Degree
  READ ( 5 , * ) DihedralAngleTolerance !in Degree
  READ ( 5 , * ) ImproperAngleTolerance !in Degree
  READ ( 5 , * ) ImproperConsiderationTolerance !in Degree
  READ ( 5 , * ) TotalNumberOfAtomTypes
   ALLOCATE ( TypeAndSymbolList ( TotalNumberOfAtomTypes , 2 ) )
   DO i = 1 , TotalNumberOfAtomTypes
    READ ( 5 , * ) TypeAndSymbolList ( i , : )
   ENDDO
  READ ( 5 , * ) NumberOfManualvdWRadiusChanges
   ALLOCATE ( vdWRadiusList ( TotalNumberOfAtomTypes ) )
   vdWRadiusList = 1.0D+8
   DO i = 1 , NumberOfManualvdWRadiusChanges
    READ ( 5 , * ) VariableAtomName , DPValue
     DO j = 1 , TotalNumberOfAtomTypes
      IF ( TRIM ( VariableAtomName ) .EQ. TRIM ( TypeAndSymbolList ( j , 1 ) ) ) THEN
        vdWRadiusList ( j ) = DPValue
      ENDIF
     ENDDO
   ENDDO
  READ ( 5 , * ) OutputFilesPrefix 
  OutputLogFileName = TRIM(OutputFilesPrefix)//".log"
 ENDSUBROUTINE ReadInputFile

 SUBROUTINE WriteInputFileToLog ()
  INTEGER                 :: i , j
  OPEN ( 61 , FILE = TRIM ( OutputLogFileName ) , ACTION = 'WRITE' , STATUS = 'NEW' )
  WRITE ( 61 , * ) "---------------------------Inputs supplied--------------------------------------"
  WRITE ( 61 , * ) "XYZFileName = ", XYZFileName
  WRITE ( 61 , * ) "ParameterFileName = ", ParameterFileName
  WRITE ( 61 , * ) "WantBonds = ", WantBonds
  WRITE ( 61 , * ) "WantAngles = ", WantAngles
  WRITE ( 61 , * ) "WantDihedrals = ", WantDihedrals
  WRITE ( 61 , * ) "WantImpropers = ", WantImpropers
  WRITE ( 61 , * ) "BondLengthTolerance !in Angstrom = ", BondLengthTolerance !in Angstrom
  WRITE ( 61 , * ) "AngleAngleTolerance !in Degree = ", AngleAngleTolerance !in Degree
  WRITE ( 61 , * ) "DihedralAngleTolerance !in Degree = ", DihedralAngleTolerance !in Degree
  WRITE ( 61 , * ) "ImproperAngleTolerance !in Degree = ", ImproperAngleTolerance !in Degree
  WRITE ( 61 , * ) "ImproperConsiderationTolerance !in Degree = ", ImproperConsiderationTolerance !in Degree
  WRITE ( 61 , * ) "TotalNumberOfAtomTypes = ", TotalNumberOfAtomTypes
  WRITE ( 61 , * ) "NumberOfManualvdWRadiusChanges = ", NumberOfManualvdWRadiusChanges
  WRITE ( 61 , * ) "Atom types, symbol and vdW radius (0.000 radius means the values are taken from the default radii list)"
   DO i = 1 , TotalNumberOfAtomTypes
    WRITE ( 61 , * ) TypeAndSymbolList ( i , : ) , vdWRadiusList ( i )
   ENDDO
  WRITE ( 61 , * ) "OutputFilesPrefix  = ", OutputFilesPrefix 
  WRITE ( 61 , * ) "---------------------------End of Input-----------------------------------------"
  WRITE ( 61 , * ) ""
  WRITE ( 61 , * ) ""
  CLOSE (61)
 ENDSUBROUTINE WriteInputFileToLog

 SUBROUTINE WriteAtomicDataToLog ()
  INTEGER                 :: i
  OPEN ( 61 , FILE = TRIM ( OutputLogFileName ) , ACTION = 'WRITE' , STATUS = 'OLD', POSITION = 'APPEND')
  WRITE ( 61 , * )"TypeIndex, Type, Symbol, Atomic Number, Mass, Charge, vdWRadius, Coordinates, &
                   ScaledCoordinates, NBonds, NAngles, NDihedrals, NImpropers"
  DO i = 1 , TotalNumberOfAtoms
   WRITE ( 61 , '(I5,X,A5,X,A4,X,I3,3(X,F7.3),X,3(F8.3,X),3(F8.5,X),4(X,I2))' ) AtomicData(i)
  ENDDO
  CLOSE (61)
 ENDSUBROUTINE WriteAtomicDataToLog

 SUBROUTINE WritePDBFile ()
  INTEGER :: i
  OPEN ( 62 , FILE = TRIM(OutputFilesPrefix)//".pdb", ACTION = 'WRITE')
  WRITE ( 62 , '(A6,3F9.3,3F7.2,X,A11,A)' ) "CRYST1",BoxParameters,"P 1","1"

  DO i = 1 , TotalNumberOfAtoms
   WRITE ( 62 , '("ATOM  ",I5,X,A4,"     X   1   ",X,3F8.3,"  0.00  0.00",10X,A2)' )&
   i , TRIM(AtomicData(i)%AtomicType), AtomicData(i)%AtomicCoordinates, TRIM(AtomicData(i)%AtomicSymbol)
  ENDDO
  WRITE (62 , '(A)' ) "END"
 END SUBROUTINE WritePDBFile
 

 SUBROUTINE WriteLammpsDataFile ()
  ! rewrite improper section better. Only a temporary fix
  INTEGER                 :: i , j
  INTEGER                 :: ImproperFactor = 3
  LammpsDataFileName = "data." // TRIM ( OutputFilesPrefix )
  OPEN ( 62 , FILE = TRIM ( LammpsDataFileName ) , ACTION = 'WRITE' , STATUS = 'NEW')
  WRITE ( 62, '(A)' ) "LAMMPS data file generated by pbc_datagen"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfAtoms , "atoms"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfBonds , "bonds"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfAngles , "angles"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfDihedrals , "dihedrals"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfImpropers * ImproperFactor, "impropers"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfAtomTypes , "atom types"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfBondTypes , "bond types"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfAngleTypes , "angle types"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfDihedralTypes , "dihedral types"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfImproperTypes , "improper types"
  WRITE (62 , * ) ""
  !Box information
  WRITE ( 62 ,'(2(F9.3,X),10X,A)' ) LammpsBoxParameters (1:2), "xlo xhi"
  WRITE ( 62 ,'(2(F9.3,X),10X,A)' ) LammpsBoxParameters (3:4), "ylo yhi"
  WRITE ( 62 ,'(2(F9.3,X),10X,A)' ) LammpsBoxParameters (5:6), "zlo zhi"
  WRITE ( 62 ,'(3(F9.3,X),A)' ) LammpsBoxParameters (7:9), "xy xz yz"
  WRITE (62 , * ) ""
  !Atom List
  WRITE (62 , * ) "Atoms"
  WRITE (62 , * ) ""
  DO i = 1 , TotalNumberOfAtoms
   WRITE ( 62 , '(I5,X,I1,X,I5,X,F8.6,3(X,F7.3),2X,2(X,A))' )&
   i , 1 , AtomicData(i)%AtomicTypeIndex , AtomicData(i)%AtomicCharge , AtomicData(i)%AtomicCoordinates &
   , "#", TRIM(AtomicData(i)%AtomicType)
  ENDDO
  WRITE (62 , * ) ""

  !Bond List
  WRITE (62 , * ) "Bonds"
  WRITE (62 , * ) ""
  DO i = 1 , TotalNumberOfBonds
   WRITE ( 62 , '(I5,3(X,I5),3X,A,X,A,"-",A,X,F6.3)' )&
   i , BondData(i)%BondType , BondData(i)%BondAtom &
   , "#", TRIM(AtomicData(BondData(i)%BondAtom(1))%AtomicType), TRIM(AtomicData(BondData(i)%BondAtom(2))%AtomicType) &
   , BondData(i)%BondDistance
  ENDDO
  WRITE (62 , * ) ""

  !Angle List
  WRITE (62 , * ) "Angles"
  WRITE (62 , * ) ""
  DO i = 1 , TotalNumberOfAngles
   WRITE ( 62 , '(I5,4(X,I5),3X,A,X,A,"-",A,"-",A,X,F6.2)' )&
   i , AngleData(i)%AngleType , AngleData(i)%AngleAtom &
   , "#", TRIM(AtomicData(AngleData(i)%AngleAtom(1))%AtomicType), TRIM(AtomicData(AngleData(i)%AngleAtom(2))%AtomicType) &
   ,TRIM(AtomicData(AngleData(i)%AngleAtom(3))%AtomicType) &
   , AngleData(i)%AngleAngle
  ENDDO
  WRITE (62 , * ) ""

  !Dihedral List
  WRITE (62 , * ) "Dihedrals"
  WRITE (62 , * ) ""
  DO i = 1 , TotalNumberOfDihedrals
   WRITE ( 62 , '(I5,5(X,I5),3X,A,X,A,"-",A,"-",A,"-",A,X,F6.2)' )&
   i , DihedralData(i)%DihedralType , DihedralData(i)%DihedralAtom &
   , "#", TRIM(AtomicData(DihedralData(i)%DihedralAtom(1))%AtomicType)&
   , TRIM(AtomicData(DihedralData(i)%DihedralAtom(2))%AtomicType) &
   ,TRIM(AtomicData(DihedralData(i)%DihedralAtom(3))%AtomicType)&
   ,TRIM(AtomicData(DihedralData(i)%DihedralAtom(4))%AtomicType) &
   , DihedralData(i)%DihedralAngle
  ENDDO
  WRITE (62 , * ) ""

  !Improper List
  WRITE (62 , * ) "Impropers"
  WRITE (62 , * ) ""
  DO i = 1 , TotalNumberOfImpropers
   WRITE ( 62 , '(I5,5(X,I5),3X,A,X,A,"-",A,"-",A,"-",A,X,F6.2)' )&
   (3* (i-1) + 1), ImproperData(i)%ImproperType , ImproperData(i)%ImproperAtom &
   , "#", TRIM(AtomicData(ImproperData(i)%ImproperAtom(1))%AtomicType)&
   , TRIM(AtomicData(ImproperData(i)%ImproperAtom(2))%AtomicType) &
   , TRIM(AtomicData(ImproperData(i)%ImproperAtom(3))%AtomicType)&
   , TRIM(AtomicData(ImproperData(i)%ImproperAtom(4))%AtomicType) &
   , ImproperData(i)%ImproperAngle
   WRITE ( 62 , '(I5,5(X,I5),3X,A,X,A,"-",A,"-",A,"-",A,X,F6.2)' )&
   (3* (i-1) + 2), ImproperData(i)%ImproperType , ImproperData(i)%ImproperAtom &
   , "#", TRIM(AtomicData(ImproperData(i)%ImproperAtom(1))%AtomicType)&
   , TRIM(AtomicData(ImproperData(i)%ImproperAtom(3))%AtomicType) &
   , TRIM(AtomicData(ImproperData(i)%ImproperAtom(4))%AtomicType)&
   , TRIM(AtomicData(ImproperData(i)%ImproperAtom(2))%AtomicType) &
   , ImproperData(i)%ImproperAngle
   WRITE ( 62 , '(I5,5(X,I5),3X,A,X,A,"-",A,"-",A,"-",A,X,F6.2)' )&
   (3* (i-1) + 3), ImproperData(i)%ImproperType , ImproperData(i)%ImproperAtom &
   , "#", TRIM(AtomicData(ImproperData(i)%ImproperAtom(1))%AtomicType)&
   , TRIM(AtomicData(ImproperData(i)%ImproperAtom(4))%AtomicType) &
   , TRIM(AtomicData(ImproperData(i)%ImproperAtom(2))%AtomicType)&
   , TRIM(AtomicData(ImproperData(i)%ImproperAtom(3))%AtomicType) &
   , ImproperData(i)%ImproperAngle
  ENDDO
  WRITE (62 , * ) ""

  CLOSE (62)

  !write a parameter file

  LammpsParameterFileName = "parm." // TRIM ( OutputFilesPrefix )
  OPEN ( 62 , FILE = TRIM ( LammpsParameterFileName ) , ACTION = 'WRITE' , STATUS = 'NEW')
  WRITE ( 62 , '(A)' ) "#Masses"
  DO j = 1 , TotalNumberOfAtomTypes
   DO i = 1 , TotalNumberOfAtoms
    IF ( j .EQ. AtomicData(i)%AtomicTypeIndex ) THEN
     WRITE ( 62 , '(A4,X,I5,X,F7.3,4X,A1,X,A5)' ) "mass" , j, AtomicData(i)%AtomicMass , "#" , AtomicData(i)%AtomicType
     EXIT
    ENDIF
   ENDDO
  ENDDO
  WRITE ( 62 , '(A)' ) "#Pair Coefficients"
  DO j = 1 , TotalNumberOfAtomTypes
   DO i = 1 , TotalNumberOfAtoms
    IF ( j .EQ. AtomicData(i)%AtomicTypeIndex ) THEN
     WRITE ( 62 , '(A10,X,I5,X,I5,4X,A1,X,A5)' ) "pair_coeff" , j , j , "#" , AtomicData(i)%AtomicType
     EXIT
    ENDIF
   ENDDO
  ENDDO
  WRITE ( 62 , '(A)' ) "#Bond Coefficients"
  DO i = 1 , TotalNumberOfBondTypes
   WRITE ( 62 , '(A10,X,I5,4X,A1,X,A,"-",A)' ) "bond_coeff" , i , "#" &
   , TRIM(TypeAndSymbolList (BondTypeData(i)%BondAtomType(1),1)), TRIM(TypeAndSymbolList (BondTypeData(i)%BondAtomType(2),1))
  ENDDO
  WRITE ( 62 , '(A)' ) "#Angle Coefficients"
  DO i = 1 , TotalNumberOfAngleTypes
   WRITE ( 62 , '(A11,X,I5,4X,A1,X,A,"-",A,"-",A)' ) "angle_coeff" , i , "#" &
   , TRIM(TypeAndSymbolList (AngleTypeData(i)%AngleAtomType(1),1))&
   , TRIM(TypeAndSymbolList (AngleTypeData(i)%AngleAtomType(2),1))&
   , TRIM(TypeAndSymbolList (AngleTypeData(i)%AngleAtomType(3),1))
  ENDDO
  WRITE ( 62 , '(A)' ) "#Dihedral Coefficients"
  DO i = 1 , TotalNumberOfDihedralTypes
   WRITE ( 62 , '(A14,X,I5,4X,A1,X,A,"-",A,"-",A,"-",A)' ) "dihedral_coeff" , i , "#" &
   , TRIM(TypeAndSymbolList (DihedralTypeData(i)%DihedralAtomType(1),1))&
   , TRIM(TypeAndSymbolList (DihedralTypeData(i)%DihedralAtomType(2),1))&
   , TRIM(TypeAndSymbolList (DihedralTypeData(i)%DihedralAtomType(3),1))&
   , TRIM(TypeAndSymbolList (DihedralTypeData(i)%DihedralAtomType(4),1))
  ENDDO
  WRITE ( 62 , '(A)' ) "#Improper Coefficients"
  DO i = 1 , TotalNumberOfImproperTypes
   WRITE ( 62 , '(A14,X,I5,4X,A1,X,A,"-",A,"-",A,"-",A)' ) "improper_coeff" , i , "#" &
   , TRIM(TypeAndSymbolList (ImproperTypeData(i)%ImproperAtomType(1),1))&
   , TRIM(TypeAndSymbolList (ImproperTypeData(i)%ImproperAtomType(2),1))&
   , TRIM(TypeAndSymbolList (ImproperTypeData(i)%ImproperAtomType(3),1))&
   , TRIM(TypeAndSymbolList (ImproperTypeData(i)%ImproperAtomType(4),1))
  ENDDO
  CLOSE (62)
  
 ENDSUBROUTINE WriteLammpsDataFile
 
 SUBROUTINE VerboseData ()
  IMPLICIT NONE
  INTEGER :: i , j , Counter , NBonds

  !Write Bond data
  DO j = 1 , TotalNumberOfBondTypes
   NBonds = 0
   Counter = 0
   WRITE ( VariableFileName, '(A,A,A,I0)') "bond-",TRIM(OutputFilesPrefix),".", j
   OPEN ( 62 , FILE = TRIM ( VariableFileName ) , ACTION = 'WRITE' , STATUS = 'NEW')
   DO i = 1 , TotalNumberOfBonds
    IF (BondData(i)%BondType .EQ. j) NBonds = NBonds + 1
   ENDDO
   CALL WriteHeaderDataFile ( NBonds )
   DO i = 1 , TotalNumberOfBonds
    IF (BondData(i)%BondType .EQ. j)THEN
     counter = counter + 1
     WRITE ( 62 , '(I5,3(X,I5),3X,A,X,A,"-",A,X,F6.3)' )&
     counter , BondData(i)%BondType , BondData(i)%BondAtom &
     , "#", TRIM(AtomicData(BondData(i)%BondAtom(1))%AtomicType), TRIM(AtomicData(BondData(i)%BondAtom(2))%AtomicType) &
     , BondData(i)%BondDistance
    ENDIF 
   ENDDO
   CLOSE ( 62 )
  ENDDO

  !Write Angle data
  DO j = 1 , TotalNumberOfAngleTypes
   NBonds = 0
   Counter = 0
   WRITE ( VariableFileName, '(A,A,A,I0)') "angle-",TRIM(OutputFilesPrefix),".", j
   OPEN ( 62 , FILE = TRIM ( VariableFileName ) , ACTION = 'WRITE' , STATUS = 'NEW')
   DO i = 1 , TotalNumberOfAngles
    IF (AngleData(i)%AngleType .EQ. j) NBonds = NBonds + 1
   ENDDO
   NBonds = NBonds * 2
   CALL WriteHeaderDataFile ( NBonds )
   DO i = 1 , TotalNumberOfAngles
    IF (AngleData(i)%AngleType .EQ. j)THEN
     counter = counter + 1
     WRITE ( 62 , '(I5,3(X,I5),3X,A,X,A,"-",A,"-",A,X,F8.3)' )&
     counter , AngleData(i)%AngleType , AngleData(i)%AngleAtom(1:2) &
     , "#", TRIM(AtomicData(AngleData(i)%AngleAtom(1))%AtomicType)&
     , TRIM(AtomicData(AngleData(i)%AngleAtom(2))%AtomicType) &
     , TRIM(AtomicData(AngleData(i)%AngleAtom(3))%AtomicType) &
     , AngleData(i)%AngleAngle
     counter = counter + 1
     WRITE ( 62 , '(I5,3(X,I5))' )&
     counter , AngleData(i)%AngleType , AngleData(i)%AngleAtom(2:3)
    ENDIF 
   ENDDO
   CLOSE ( 62 )
  ENDDO

  !Write Dihedral data
  DO j = 1 , TotalNumberOfDihedralTypes
   NBonds = 0
   Counter = 0
   WRITE ( VariableFileName, '(A,A,A,I0)') "dihedral-",TRIM(OutputFilesPrefix),".", j
   OPEN ( 62 , FILE = TRIM ( VariableFileName ) , ACTION = 'WRITE' , STATUS = 'NEW')
   DO i = 1 , TotalNumberOfDihedrals
    IF (DihedralData(i)%DihedralType .EQ. j) NBonds = NBonds + 1
   ENDDO
   NBonds = NBonds * 3
   CALL WriteHeaderDataFile ( NBonds )
   DO i = 1 , TotalNumberOfDihedrals
    IF (DihedralData(i)%DihedralType .EQ. j)THEN
     counter = counter + 1
     WRITE ( 62 , '(I5,3(X,I5),3X,A,X,A,"-",A,"-",A,"-",A,X,F8.3)' )&
     counter , DihedralData(i)%DihedralType , DihedralData(i)%DihedralAtom(1:2) &
     , "#"&
     , TRIM(AtomicData(DihedralData(i)%DihedralAtom(1))%AtomicType)&
     , TRIM(AtomicData(DihedralData(i)%DihedralAtom(2))%AtomicType) &
     , TRIM(AtomicData(DihedralData(i)%DihedralAtom(3))%AtomicType) &
     , TRIM(AtomicData(DihedralData(i)%DihedralAtom(4))%AtomicType) &
     , DihedralData(i)%DihedralAngle
     counter = counter + 1
     WRITE ( 62 , '(I5,3(X,I5))' )&
     counter , DihedralData(i)%DihedralType , DihedralData(i)%DihedralAtom(2:3)
     counter = counter + 1
     WRITE ( 62 , '(I5,3(X,I5))' )&
     counter , DihedralData(i)%DihedralType , DihedralData(i)%DihedralAtom(3:4)
    ENDIF 
   ENDDO
   CLOSE ( 62 )
  ENDDO

  !Write Improper data
  DO j = 1 , TotalNumberOfImproperTypes
   NBonds = 0
   Counter = 0
   WRITE ( VariableFileName, '(A,A,A,I0)') "improper-",TRIM(OutputFilesPrefix),".", j
   OPEN ( 62 , FILE = TRIM ( VariableFileName ) , ACTION = 'WRITE' , STATUS = 'NEW')
   DO i = 1 , TotalNumberOfImpropers
    IF (ImproperData(i)%ImproperType .EQ. j) NBonds = NBonds + 1
   ENDDO
   NBonds = NBonds * 3
   CALL WriteHeaderDataFile ( NBonds )
   DO i = 1 , TotalNumberOfImpropers
    IF (ImproperData(i)%ImproperType .EQ. j)THEN
     counter = counter + 1
     WRITE ( 62 , '(I5,3(X,I5),3X,A,X,A,"-",A,"-",A,"-",A,X,F8.3)' )&
     counter , ImproperData(i)%ImproperType , ImproperData(i)%ImproperAtom(1:2) &
     , "#"&
     , TRIM(AtomicData(ImproperData(i)%ImproperAtom(1))%AtomicType)&
     , TRIM(AtomicData(ImproperData(i)%ImproperAtom(2))%AtomicType) &
     , TRIM(AtomicData(ImproperData(i)%ImproperAtom(3))%AtomicType) &
     , TRIM(AtomicData(ImproperData(i)%ImproperAtom(4))%AtomicType) &
     , ImproperData(i)%ImproperAngle
     counter = counter + 1
     WRITE ( 62 , '(I5,3(X,I5))' )&
     counter , ImproperData(i)%ImproperType , ImproperData(i)%ImproperAtom(1) &
                                            , ImproperData(i)%ImproperAtom(3)
     counter = counter + 1
     WRITE ( 62 , '(I5,3(X,I5))' )&
     counter , ImproperData(i)%ImproperType , ImproperData(i)%ImproperAtom(1) &
                                            , ImproperData(i)%ImproperAtom(4)
    ENDIF 
   ENDDO
   CLOSE ( 62 )
  ENDDO

 ENDSUBROUTINE VerboseData



 SUBROUTINE WriteHeaderDataFile (NBonds)
  INTEGER , INTENT (IN)   :: NBonds
  INTEGER                 :: i , j
  WRITE ( 62, '(A)' ) "LAMMPS data file generated by pbc_datagen"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfAtoms , "atoms"
  WRITE ( 62 , '(I10,X,A)' ) NBonds , "bonds"
  WRITE ( 62 , '(9X,A,X,A)' ) "0", "angles"
  WRITE ( 62 , '(9X,A,X,A)' ) "0", "dihedrals"
  WRITE ( 62 , '(9X,A,X,A)' ) "0", "impropers"
  WRITE ( 62 , '(I10,X,A)' ) TotalNumberOfAtomTypes , "atom types"
  WRITE ( 62 , '(9X,A,X,A)' ) "1", "bond types"
  WRITE ( 62 , '(9X,A,X,A)' ) "0", "angle types"
  WRITE ( 62 , '(9X,A,X,A)' ) "0", "dihedral types"
  WRITE ( 62 , '(9X,A,X,A)' ) "0", "improper types"
  WRITE (62 , * ) ""
  !Box information
  WRITE ( 62 ,'(2(F9.3,X),10X,A)' ) LammpsBoxParameters (1:2), "xlo xhi"
  WRITE ( 62 ,'(2(F9.3,X),10X,A)' ) LammpsBoxParameters (3:4), "ylo yhi"
  WRITE ( 62 ,'(2(F9.3,X),10X,A)' ) LammpsBoxParameters (5:6), "zlo zhi"
  WRITE ( 62 ,'(3(F9.3,X),A)' ) LammpsBoxParameters (7:9), "xy xz yz"
  WRITE (62 , * ) ""
  !Atom List
  WRITE (62 , * ) "Atoms"
  WRITE (62 , * ) ""
  DO i = 1 , TotalNumberOfAtoms
   WRITE ( 62 , '(I5,X,I1,X,I5,X,F8.6,3(X,F7.3),2X,2(X,A))' )&
   i , 1 , AtomicData(i)%AtomicTypeIndex , AtomicData(i)%AtomicCharge , AtomicData(i)%AtomicCoordinates &
   , "#", TRIM(AtomicData(i)%AtomicType)
  ENDDO
  WRITE (62 , * ) ""

  !Bond List
  WRITE (62 , * ) "Bonds"
  WRITE (62 , * ) ""
 ENDSUBROUTINE WriteHeaderDataFile

ENDMODULE IOModule
